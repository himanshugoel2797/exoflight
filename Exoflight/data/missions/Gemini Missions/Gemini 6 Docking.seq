
sequence "Gemini 6 Docking" {

	ship.name = "Gemini 6";

	@reentry = ship.structure.'$Gemini Reentry';
	@equip = ship.structure.'$Gemini Equipment';

-- prepare target zone

	@targzone = game.agency.newbase;
	@targzone.name = "Recovery Ship";
	@targzone.modelname = "Navy Destroyer";

	@traj = new LandedTrajectory;
	@traj.parent = game.$Earth;
	@traj.landpos = [-75.5 deg, 28 deg, 0];
	@targzone.traj = @traj;

-- prepare Gemini

	@orbelem = new KeplerianElements;
	@orbelem.eccent = 0.00031;
	@orbelem.periapsis = 6676;
	@orbelem.incl = 28.8691 deg; -- ??
	@orbelem.raan = 158.7132 deg;
	@orbelem.argperi = -169.020 deg;
	@orbelem.trueanom = 9 deg;
	@orbelem.epochticks = -1100150968320;

	@traj = new CowellTrajectory;
	@traj.parent = game.$Earth;
	@traj.orbelemgeo = @orbelem;
	@ort = new Orientation;
	@ort.yaw = 180 deg;
	@traj.velrefort = @ort;
	ship.traj = @traj;

-- prepare Agena Target Vehicle

	@orbelem = new KeplerianElements;
	@orbelem.eccent = 0.00031;
	@orbelem.periapsis = 6676; -- must be after eccent set
	@orbelem.incl = 28.869 deg; -- ??
	@orbelem.raan = 158.713 deg;
	@orbelem.argperi = -169.023 deg;
	@orbelem.trueanom = 9 deg;
	@orbelem.epochticks = -1100150968320;

	@agena = game.agency.'preparevehicle$Agena Target Vehicle';

	@traj = new CowellTrajectory;
	@traj.parent = game.$Earth;
	@traj.orbelemgeo = @orbelem;

	@ort = new Orientation;
	@traj.velrefort = @ort;
	@agena.traj = @traj;

	-- set atmosphere

	@atmo = new ResourceSet;
	@atmo.'#O2' = 1.1175;
--	@atmo.'#O2' = 3.2879; -- this is 101.36 Kpa, sea level

	@reentry.atmosphere = @atmo;

	-- set up crew

	game.agency.'crew#4'.module = @reentry;
	game.agency.'crew#13'.module = @reentry;

	@ui.ship =? ship;
	@ui.message =? "You have rendezvoused with the Agena Target Vehicle and are preparing to dock.";

	-- turn on life support
	@reentry.'$main bus'.active = true;
	wait for 2;
	@reentry.'$ECS'.active = true;
	@equip.'$OAMS'.armed = true;
    ship.attsys.guidmode = "aoa-aft";
    @reentry.'$guidance system'.active = true;

	ship.targsys.target = @agena;

	wait for 5;

    @ui.message =? "If you press 'C' to change to the Gemini cockpit camera, you'll see a little white speck in the distance. That is the Agena Target Vehicle to which you will dock.";

    wait for 10;
    
	@ui.message =? "Select the DOCKING panel on your MEDS.  This will display two crosshairs that will help you navigate.  Also press the '100 m' button on the MEDS panel.";

	wait for 10;

	@ui.message =? "Now turn on your docking radar.  Find the PROBE switch on the Gemini Reentry module panel, and flip it up.";

	wait for condition (@reentry.$probe.active == true) interval 1 timeout 60;

    @ui.message =? "Note the blue line on the left crosshair. This shows the position of the target relative to your spacecraft. It should be pointing straight down, which means the target is dead ahead.";
    
    wait for 10;

    @ui.message =? "The yellow line indicates velocity. Your goal is to move the yellow line so that it is directly opposite the blue line, until the blue line disappears.";

    wait for 10;

    @ui.message =? "Briefly press the Ctrl+PgUp key. This will begin moving your spacecraft towards the target.";

    wait for condition (ship.targsys.velvec.z < -0.0001) interval 1 timeout 60;

    @ui.message =? "Note that the yellow line on the left is now pointing up. This indicates you are moving toward the target.";

    wait for 10;

    @ui.message =? "At the bottom of the MEDS, the Z value shows the distance to the target (in the forward direction) and the \0129Z value shows the rate of closure (negative means you are approaching the target). Hold down the Ctrl+PgUp key until \0129Z reaches -5 m/s.";
    
    wait for condition (ship.targsys.velvec.z < -0.005) interval 1 timeout 300;

    @ui.message =? "Good. You are slowly approaching the target vehicle.";

    wait for 10;

    @ui.message =? "While the crosshairs on the left show a top-down view of your spacecraft, those on the right show a forward-facing view. Now use the Ctrl + up, down, left, and right keys on the keypad to move the yellow line on the right crosshair opposite the blue line.";

    wait for condition (ship.targsys.dockvec.z < 0.060) interval 1 timeout 300;

    @ui.message =? "You are now about 50 meters from the target. Switch to close-range radar by pressing the '20 m' button on the MEDS.";
    
    wait for 10;
    
    @ui.message =? "Start slowing down by holding down the Ctrl+Home key (reverse thrust) until \0129Z gets less than 1 m/s.";
    
    wait for condition (ship.targsys.velvec.z < -0.001) interval 1 timeout 60;

    @ui.message =? "If the blue line on the left crosshair is still facing down and the blue line on the right crosshair is centered, you should have the target in view. Continue to approach slowly and you will automatically dock when you touch the target vehicle.";
    
    wait for condition (@agena.ship == ship) interval 1 timeout 600;
    
    @ui.message =? "Good job! In the future, you can automate the docking process by pressing the 'Activate' button on the MEDS."; 
}

