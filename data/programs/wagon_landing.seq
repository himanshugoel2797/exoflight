
sequence "Space Wagon Landing" {

	@mod = ship.structure.'$Space Wagon';

	wait until -15;
"Approach program enable"
	ship.attsys.throtmanual = 0;
	@mod.'$main engine'.armed = true;
	ship.guidancecap.settingidx = 0;
	
	ship.attsys.guidmode = "approach";
	ship.attctrl.guidance.timebias = 15;
	ship.attctrl.guidance.heightbias = 0.1;
	@mod.$RCS.armed = true;
	@mod.'$guidance system'.active = true;

"Wait for attitude lock"
	wait for condition (ship.attctrl.locked == true) interval 0.25 timeout 300;
	wait for 1;

	wait until 0;
"Begin braking phase"
	ship.attctrl.guidance.adjthrottle = true;

	wait for condition (ship.attctrl.guidance.timerem < 1) interval 0.5 timeout 300;
"Convert to hover mode"
	ship.attctrl.guidance.pitch = -90 deg;
	ship.attctrl.guidance.heightbias = -0.02;
	@mod.'$main engine'.armed = false;
	wait for 2;
	@mod.'$hover engine'.armed = true;

	wait for condition (ship.telemetry.altagl < 0.006) interval 0.5 timeout 1200;
"Engine stop"
	@mod.'$hover engine'.armed = false;
	@mod.'$guidance system'.active = false;
	ship.attsys.throtmanual = 1;

	wait for condition (ship.trajectory.type == "landed") interval 0.125 timeout 10;
"---END LANDING SEQUENCE---"
	stop;
	
}

